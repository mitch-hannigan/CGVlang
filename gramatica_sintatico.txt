S -> EXPR S | type TYPEDECL S | get GETFUNC S | put PUTFUNC S | doif DOIF S | do_it_again DO_IT_AGAIN S | do_it_again_until DO_IT_AGAIN_UNTIL S | ϵ

EXPR -> EXPR_LO_OR

EXPR_LO_OR -> EXPR_LO_AND EXPR_LO_OR_REST
EXPR_LO_OR_REST -> ortoken EXPR_LO_AND EXPR_LO_OR_REST | ϵ

EXPR_LO_AND -> EXPR_COMP_GE EXPR_LO_AND_REST
EXPR_LO_AND_REST -> andtoken EXPR_COMP_GE EXPR_LO_AND_REST | ϵ

EXPR_COMP_GE -> EXPR_COMP_LE EXPR_COMP_GE_REST
EXPR_COMP_GE_REST -> >= EXPR_COMP_LE EXPR_COMP_GE_REST | ϵ

EXPR_COMP_LE -> EXPR_COMP_G EXPR_COMP_LE_REST
EXPR_COMP_LE_REST -> <= EXPR_COMP_G EXPR_COMP_LE_REST | ϵ

EXPR_COMP_G -> EXPR_COMP_L EXPR_COMP_G_REST
EXPR_COMP_G_REST -> > EXPR_COMP_L EXPR_COMP_G_REST | ϵ

EXPR_COMP_L -> EXPR_COMP_E EXPR_COMP_L_REST
EXPR_COMP_L_REST -> < EXPR_COMP_E EXPR_COMP_L_REST | ϵ

EXPR_COMP_E -> EXPR_ARIT_AS EXPR_COMP_E_REST
EXPR_COMP_E_REST -> == EXPR_ARIT_AS EXPR_COMP_E_REST | ϵ

EXPR_ARIT_AS ->  EXPR_ARIT_MD EXPR_ARIT_AS_REST
EXPR_ARIT_AS_REST -> + EXPR_ARIT_MD EXPR_ARIT_AS_REST | - EXPR_ARIT_MD EXPR_ARIT_AS_REST | ϵ

EXPR_ARIT_MD -> EXPR_ARIT_EXP EXPR_ARIT_MD_REST
EXPR_ARIT_MD_REST -> * EXPR_ARIT_EXP EXPR_ARIT_MD_REST | / EXPR_ARIT_EXP EXPR_ARIT_MD_REST | ϵ

EXPR_ARIT_EXP -> EXPR_NOT EXPR_ARIT_EXP_REST
EXPR_ARIT_EXP_REST -> ** EXPR_ARIT_EXP | ϵ

EXPR_NOT -> ~ EXPR_NOT | EXPR_FINAL

EXPR_FINAL -> val | ( EXPR ) | id EXPR_FINAL_REST
EXPR_FINAL_REST -> = EXPR | ϵ

TYPEDECL -> id TYPEDECL_ATTRIB

TYPEDECL_ATTRIB -> = EXPR | ϵ

GETFUNC -> ( id )

PUTFUNC -> ( EXPR )

DOIF -> ( EXPR ) { S } MAYBE_NOT

MAYBE_NOT -> maybe_not { S } | ϵ

DO_IT_AGAIN -> ( EXPR ) { S }

DO_IT_AGAIN_UNTIL -> ( id , EXPR , EXPR , EXPR ) { S }
